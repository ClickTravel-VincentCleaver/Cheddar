apply from: '../../test.gradle'
apply from: '../../logging-api.gradle'

def springVersion = '4.1.1.RELEASE'
def awsSdkVersion = '1.11.6'

dependencies {
    compile project(':commons:commons-lang')
    compile project(':commons:commons-httpclient')
    compile project(':cheddar:cheddar-messaging')
    compile project(':cheddar:cheddar-persistence')

    compile "com.amazonaws:aws-java-sdk-s3:${awsSdkVersion}"
    compile "com.amazonaws:aws-java-sdk-dynamodb:${awsSdkVersion}"
    compile "com.amazonaws:aws-java-sdk-ses:${awsSdkVersion}"
    compile "com.amazonaws:aws-java-sdk-sns:${awsSdkVersion}"
    compile "com.amazonaws:aws-java-sdk-sqs:${awsSdkVersion}"
    compile "com.amazonaws:aws-java-sdk-cloudsearch:${awsSdkVersion}"
    compile "com.amazonaws:aws-java-sdk-cloudwatch:${awsSdkVersion}"
    compile "com.amazonaws:aws-java-sdk-lambda:${awsSdkVersion}"
    compile "org.springframework:spring-context-support:${springVersion}"
    compile "org.springframework:spring-aop:${springVersion}"

    compile "org.aspectj:aspectjrt:1.7.4"
    compile "org.aspectj:aspectjweaver:1.7.4"
    compile 'javax.mail:mail:1.4.7'
    compile 'joda-time:joda-time:2.8.2'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.6.3'
    compile 'net.spy:spymemcached:2.11.4' // Used to connect to memcache nodes - the aws version of this file is not in a public repo
}

test {
    useJUnit {
        excludeCategories 'com.clicktravel.infrastructure.integration.aws.AwsIntegration'
    }
}

processTestResources {
    filter(org.apache.tools.ant.filters.ReplaceTokens,
        tokens: [
            AWS_ACCESS_KEY_ID: aws_integration_access_key_id,
            AWS_SECRET_KEY_ID: aws_integration_secret_key,
            AWS_DYNAMODB_ENDPOINT: aws_dynamodb_endpoint,
            AWS_CLOUDSEARCH_ENDPOINT: aws_cloudsearch_endpoint
        ])
}

task integrationTest(type: Test) {
    useJUnit {
        includeCategories 'com.clicktravel.infrastructure.integration.aws.AwsIntegration'
    }
}
