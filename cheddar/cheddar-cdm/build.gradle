// Enable XJCTask to be added to Ant's xjc task
configurations { jaxb }
dependencies { jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.4-1' }

def jaxbClassesDir = file('build/generated/jaxb')
def jaxbSchemaDir = file('src/main/xsd/tactical')
def jaxbBindingsDir = file('src/main/xjb')
def exampleDocsDir = file('src/test/xsd/tactical')

// Add source directory for IntelliJ IDEA
idea {
	module {
		excludeDirs -= file('build')
		sourceDirs += jaxbClassesDir
	}
}

// Create directory for JAXB source generated by Ant xjc task
task mkGenDirs << {
	jaxbClassesDir.mkdirs()
}

// Generate JAXB source from schema files using Ant xjc task
task xjc(dependsOn: [processResources, mkGenDirs])  {
	inputs.dir(jaxbSchemaDir)
	inputs.dir(jaxbBindingsDir)
	outputs.dir(jaxbClassesDir)
	doLast{
		ant.taskdef (name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.jaxb.asPath)
		ant.xjc (destdir: jaxbClassesDir, extension: true) {
			schema (dir: jaxbSchemaDir, includes: '*.xsd')
			binding (dir: jaxbBindingsDir, includes: '*.xjb')
		}
	}
}

// Add generated JAXB source to Java source to compile 
sourceSets {
    main {
        java { srcDir jaxbClassesDir }
    }
}

compileJava.dependsOn(xjc)

// Validate example documents against schema; fail build if validation fails
task schemacheck << {
    ant.schemavalidate {
        fileset (dir: exampleDocsDir, includes: '**/*.xml')
    }
}

check.dependsOn(schemacheck)
